/*
 *  Copyright 2021, TeamDev. All rights reserved.
 *  
 *  Redistribution and use in source and/or binary forms, with or without
 *  modification, must retain the above copyright notice and the following
 *  disclaimer.
 *  
 *  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 *  "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 *  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 *  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 *  OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 *  SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 *  LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 *  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 *  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 *  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 *  OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    // This plugin automatically resolves SWT dependencies.
    id 'com.diffplug.gradle.eclipse.mavencentral' version '3.18.0' apply false
}

ext {
    jxBrowserVersion = '7.19' // The version of JxBrowser used in the examples.
    guavaVersion = '29.0-jre' // Some of the examples use Guava.
}

allprojects {
    if (!name.startsWith('eclipse')) {
        apply plugin: 'idea'
    }

    group = 'com.teamdev.jxbrowser-examples'
    version = "$jxBrowserVersion"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.gradle.eclipse.mavencentral'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        maven { url = 'https://europe-maven.pkg.dev/jxbrowser/releases' }
    }

    dependencies {
        // Cross-platform dependency
        // -------------------------
        compile "com.teamdev.jxbrowser:jxbrowser-cross-platform:${jxBrowserVersion}"

        /*
           For having only platform-dependent dependency:
           1. comment out the cross-platform dependency above;
           2. uncomment the dependency for your platform.
        */

        // Windows 32-bit
        // -------------------------
        // compile "com.teamdev.jxbrowser:jxbrowser-win32:${jxBrowserVersion}"

        // Windows 64-bit
        // -------------------------
        // compile "com.teamdev.jxbrowser:jxbrowser-win64:${jxBrowserVersion}"

        // macOS 64-bit
        // -------------------------
        // compile "com.teamdev.jxbrowser:jxbrowser-mac:${jxBrowserVersion}"

        // macOS 64-bit ARM
        // -------------------------
        // compile "com.teamdev.jxbrowser:jxbrowser-mac-arm:${jxBrowserVersion}"

        // Linux 64-bit
        // -------------------------
        // compile "com.teamdev.jxbrowser:jxbrowser-linux64:${jxBrowserVersion}"

        // Linux 64-bit ARM
        // -------------------------
        // compile "com.teamdev.jxbrowser:jxbrowser-linux64-arm:${jxBrowserVersion}"

        // JavaFX dependency
        // -------------------------
        compile "com.teamdev.jxbrowser:jxbrowser-javafx:${jxBrowserVersion}"

        // Swing dependency
        // -------------------------
        compile "com.teamdev.jxbrowser:jxbrowser-swing:${jxBrowserVersion}"

        // SWT dependency
        // -------------------------
        compile "com.teamdev.jxbrowser:jxbrowser-swt:${jxBrowserVersion}"

        // Depend on Guava for the Resources utility class used for loading resource files into strings.
        compile "com.google.guava:guava:$guavaVersion"

        compile files("$rootDir/examples/src/main/resources/resource.jar")
    }

    tasks.withType(JavaExec) {
        // Assign all Java system properties from
        // the command line to the JavaExec task.
        systemProperties System.properties
    }

    eclipseMavenCentral {
        // Plugin documentation claims that they support versions 3.5.0 through 4.12.0.
        // Nevertheless all versions higher than 4.8.0 cannot be resolved.
        // Current version is bundled with the 3.107.0 version of the SWT.
        release '4.8.0', {
            compile 'org.eclipse.swt'
            useNativesForRunningPlatform()
        }
    }
}

// IDEA project configuration.
idea {
    project {
        ipr {
            beforeMerged { final project ->
                project.modulePaths.clear()
            }
            withXml { final provider ->
                provider.node.component
                        .find { it.@name == 'VcsDirectoryMappings' }
                        .mapping.@vcs = 'Git'
            }
        }
    }

    module {
        downloadJavadoc = true
    }
}
